# Maintainer: eloaders <eloaders at linux dot pl>

pkgname=i-nex-git
pkgver=7.0.0.85.g7b583e6
pkgrel=1
pkgdesc="System information tool like hardinfo, sysinfo"
arch=('i686' 'x86_64')
url="http://i-nex.linux.pl/"
license=('LGPL3')
depends=('gambas3-runtime' 'gambas3-gb-image' 'gambas3-gb-form'
         'gambas3-gb-desktop' 'gambas3-gb-settings' 'gambas3-gb-qt4'
         'gambas3-gb-form-stock' 'python2' 'libcpuid-git'
         'xorg-server-utils' 'lsb-release' 'curl' 'mesa-demos')
makedepends=('gambas3-devel' 'gcc' 'imagemagick' 'git')
source=($pkgname::'git://github.com/eloaders/I-Nex.git')
sha256sums=('SKIP')
provides=('i-nex' 'i-nex-bzr' 'i-nex-dev')
conflicts=('i-nex' 'i-nex-bzr' 'i-nex-dev')
pkgver() {
  cd $pkgname
  git describe | tr - .
}
prepare() {
  cd $pkgname
  # make it dynamic
  sed -i -e 's|^STATIC.*|STATIC = false|' i-nex.mk
  cd I-Nex
  autoreconf -i
}
 
build() {
  cd $pkgname
  cd I-Nex
  ./configure --prefix=/usr
  cd ..
  make
}
 
package() {
  cd $pkgname
  make install DESTDIR="$pkgdir"
  sed -i 's|python3$|python2|' "$pkgdir/usr/share/i-nex/pastebinit/pastebinit"
}