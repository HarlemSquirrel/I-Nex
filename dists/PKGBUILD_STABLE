pkgname=i-nex
pkgver=7.0.0
_git=7.0.0
pkgrel=1
pkgdesc="System information tool like hardinfo, sysinfo"
arch=('i686' 'x86_64')
url="http://i-nex.linux.pl/"
license=('LGPL3')
depends=('procps-ng' 'gambas3-runtime' 'gambas3-gb-image' 'gambas3-gb-gtk' 'gambas3-gb-form'
         'gambas3-gb-desktop' 'gambas3-gb-settings' 'gambas3-gb-form-dialog' 'gambas3-gb-qt4'
         'gambas3-gb-form-mdi' 'gambas3-gb-form-stock' 'util-linux' 'procps' 'python2' 'libcpuid'
         'xorg-server-utils' 'lsb-release' 'coreutils' 'pciutils' 'inetutils' 'mesa-demos' 'curl')
makedepends=('gambas3-devel' 'gcc' 'imagemagick')
source=("$pkgname-$pkgver-$pkgrel.tar.gz::https://github.com/eloaders/I-Nex/archive/$_git.tar.gz")
sha256sums=('SKIP')
replaces=(i-nex-dev)
provides=(i-nex-dev)
conflicts=(i-nex-dev)
 
prepare() {
  cd "$srcdir/I-Nex-$_git"
  # make it dynamic
  sed -i -e 's|^STATIC.*|STATIC = false|' i-nex.mk
  cd src
  autoreconf --install
  ./configure --prefix=/usr
}
 
build() {
  cd "$srcdir/I-Nex-$_git"
 
  make
}
 
package() {
  cd "$srcdir/I-Nex-$_git"
  mkdir -p "$pkgdir/usr/share/applications"
  make install DESTDIR="${pkgdir}"
} 
